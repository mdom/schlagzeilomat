#!/usr/bin/perl

use Mojo::Base -base;
use Mojo::Feed;
use Mojo::SQLite;
use Mojo::WebService::Twitter;
use Mojo::File 'path';
use Mojo::JSON 'decode_json';
use Encode;


my $config_file = path('schlagzeilomat.json');

if ( !-e $config_file ) {
    die "Missing configuration file $config_file\n";
}

my $config = decode_json $config_file->slurp;

my $sql = Mojo::SQLite->new('sqlite:test.db');
$sql->migrations->from_string(<<EOF)->migrate;
  -- 1 up
	create table items (
		id integer primary key autoincrement,
		guid text unique,
		published integer default 0,
		title text not null,
		link text not null
	);
  -- 1 down
	drop table items;
EOF

for my $url ( @{ $config->{feeds} } ) {
    $feed = Mojo::Feed->new( url => $url );

    for my $item ( $feed->items->reverse->each ) {
        $sql->db->query(
            'insert or ignore into items (guid, title, link) values (?,?,?)',
            $item->id, $item->title, $item->link );
    }
}

my $twitter = Mojo::WebService::Twitter->new(
    api_key    => $config->{api_key},
    api_secret => $config->{api_secret_key},
);

$twitter->authentication(
    oauth => $config->{access_token},
    $config->{access_token_secret},
);

my @items = $sql->db->query(
    'select * from items where published = 0 order by id limit 1 ')
  ->hashes->each;

for my $item (@items) {
    $twitter->post_tweet( $item->{title} . " " . $item->{link} );
    warn encode( 'UTF-8', "Publish " . $item->{title} . "\n" );
    $sql->db->update( items => { published => 1 }, { id => $item->{id} } );
}

exit 0;
