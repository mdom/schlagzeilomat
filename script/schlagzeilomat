#!/usr/bin/perl

use Mojo::Base -base;
use Mojo::Feed;
use Mojo::SQLite;
use Mojo::WebService::Twitter;
use Mojo::File 'path';
use Mojo::JSON 'decode_json';
use Encode;
use Fcntl qw(:flock);
use Getopt::Long;
use Pod::Usage;
use FindBin qw($Bin);
use lib "$Bin/../extlib/lib/perl5/";

open my $lock_file, '<', $0 or die $!;
flock $lock_file, LOCK_EX | LOCK_NB or die "Unable to lock file $0: $!\n";

my $options = { import => 1, publish => 1, dry_run => 0 };
GetOptions( $options, 'import!', 'publish!', 'dry_run|dry-run|n', 'help' )
  or pod2usage(1);

pod2usage(1) if $options->{help};

my $config_file = path('schlagzeilomat.json');

if ( !-e $config_file ) {
    die "Missing configuration file $config_file\n";
}

my $config = decode_json $config_file->slurp;

my $sql = Mojo::SQLite->new('sqlite:test.db');
$sql->migrations->from_string(<<EOF)->migrate;
  -- 1 up
	create table items (
		id integer primary key autoincrement,
		guid text unique,
		published integer default 0,
		title text not null,
		link text not null
	);
  -- 1 down
	drop table items;
EOF

if ( $options->{import} ) {
    for my $url ( @{ $config->{feeds} } ) {
        my $feed = Mojo::Feed->new( url => $url );

        for my $item ( $feed->items->reverse->each ) {
            $sql->db->query(
                q{ insert or ignore into items 
						(guid, title, link) values (?,?,?) },
                $item->id, $item->title, $item->link
            );
        }
    }
}

if ( $options->{publish} ) {

    my $twitter = Mojo::WebService::Twitter->new(
        api_key    => $config->{api_key},
        api_secret => $config->{api_secret_key},
    );

    $twitter->authentication(
        oauth => $config->{access_token},
        $config->{access_token_secret},
    );

    my @items = $sql->db->query(
        'select * from items where published = 0 order by id limit 1 ')
      ->hashes->each;

    for my $item (@items) {
        my $msg = encode( 'UTF-8', $item->{title} . " " . $item->{link} );
        if ( !$options->{dry_run} ) {
            eval { $twitter->post_tweet($msg) };
            if ($@) {
                warn $@->to_string;
                next;
            }
            $sql->db->update(
                items => { published => 1 },
                { id => $item->{id} }
            );
        }
        warn qq{Publish "$msg"\n};
    }
}

exit 0;

__END__

=head1 NAME

schlagzeilomat - Publish rss feeds to twitter

=head1 SYNOPSIS

schlagzeilomat [options]

=head1 OPTIONS

=over

=item --[no-]publish

Publish unpublished items to twitter. [default: true]

=item --[no-]import

Import RSS feeds. [default: true]

=item --dry-run

Print items to stderr.

=item --help

Show this help.

=back

=head1 DESCRIPTION

B<schlagzeilomat> will import RSS feeds and publish their items to
twitter.

=head1 COPYRIGHT AND LICENSE 

Copyright 2020 Mario Domgoergen C<< <mario@domgoergen.com> >> 

This program is free software: you can redistribute it and/or modify 
it under the terms of the GNU General Public License as published by 
the Free Software Foundation, either version 3 of the License, or 
(at your option) any later version. 

This program is distributed in the hope that it will be useful, 
but WITHOUT ANY WARRANTY; without even the implied warranty of 
MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the 
GNU General Public License for more details. 

You should have received a copy of the GNU General Public License 
along with this program.  If not, see <http://www.gnu.org/licenses/>. 

=cut
